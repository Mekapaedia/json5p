start: value | object_inner

value: object
        | array
        | string
        | number
        | "true"             -> true
        | "false"            -> false
        | "null"             -> null
        | "!"                -> undefined
        | set_ref
        | use_ref

number: SIGNED_NUMBER | HEX_NUMBER | OCT_NUMBER | BIN_NUMBER

array: "[" array_inner "]"
?array_inner: [value ("," value)*] ","?
object: "{" object_inner "}"
object_inner: [pair ("," pair)*] ","?
pair: key ":" value
?key: proto | string
proto: "!" cname
set_ref: "&" cname value
use_ref: "*" cname value?

string: quoted | cname
quoted: STRING
cname: CNAME

STRING: /("(?!"").*?(?<!\\)(\\\\)*?"|'(?!'').*?(?<!\\)(\\\\)*?')/is
HEX_NUMBER: "0" ("x"|"X") HEXDIGIT+
OCT_NUMBER: "0" ("o"|"O") OCTDIGIT+
BIN_NUMBER: "0" ("b"|"B") BINDIGIT+

OCTDIGIT: ("0".."7")
BINDIGIT: ("0".."1")

%import common.CPP_COMMENT
%import common.SIGNED_NUMBER
%import common.HEXDIGIT
%import common.CNAME
%import common.WS

%ignore WS
%ignore CPP_COMMENT
